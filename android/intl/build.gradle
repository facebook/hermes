/*
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

// Please note: Hermes does not yet support Intl.
//              This file is a work in progress.

apply plugin: 'com.android.library'

def facebookBuild = System.getenv("FACEBOOK") ?: "0"
def hermesHostBuild = System.getenv("HERMES_WS_DIR")
buildDir = "${rootProject.ext.hermes_ws}/build_android/intl"
buildDir.mkdirs()

// For Facebook internal use:
def fbsource = System.getenv("FBSOURCE_DIR") ?:
    System.getenv("HOME") + "/fbsource"

android {
  compileSdkVersion = 24

  defaultConfig {
    minSdkVersion = 24
    ndk {
      abiFilters (*rootProject.ext.abis)
    }

    externalNativeBuild {
      cmake {
        arguments "-DHERMES_IS_ANDROID=True"
        arguments "-DHERMES_FACEBOOK_BUILD=${facebookBuild}"
        arguments "-DANDROID_STL=c++_shared"
        arguments "-DANDROID_PIE=True"
        arguments "-DIMPORT_HERMESC=${hermes_ws}/build/ImportHermesc.cmake"
        arguments "-DFBSOURCE_DIR=${fbsource}"
        arguments "-DHERMES_SLOW_DEBUG=False"
        arguments "-DHERMES_ENABLE_INTL=True"
        targets "libhermes"
      }
    }
    ndk {
      abiFilters (*rootProject.ext.abis)
    }
  }

  externalNativeBuild {
    cmake {
      version "3.6.0"
      path "../../CMakeLists.txt"
      buildStagingDirectory = "${rootProject.ext.hermes_ws}/staging/intl"
      buildStagingDirectory.mkdirs()
    }
  }

  buildTypes {
    debug {
      externalNativeBuild {
        cmake {
          arguments "-DHERMES_ENABLE_DEBUGGER=True"
        }
      }
    }
  }

  dependencies {
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'org.easytesting:fest-assert-core:2.0M10'
    androidTestImplementation 'com.facebook.fbjni:fbjni:0.0.2'
    androidTestImplementation 'com.facebook.soloader:soloader:0.9.0'
    androidTestImplementation 'com.facebook.yoga:proguard-annotations:1.14.1'
    androidTestImplementation project(':cppruntime')
  }

  sourceSets {
    main {
      java {
        srcDirs = ["${rootProject.ext.hermes_ws}/hermes/lib/Platform/Intl/java"]
      }
    }
  }

  packagingOptions {
    exclude "**/libc++_shared.so"
  }

  afterEvaluate {
    def aarDir = "$buildDir/outputs/aar"
    tasks.named("assembleDebug").configure {
      doLast {
        file("$aarDir/intl-debug.aar").renameTo("${rootProject.ext.outputDir}/hermes-intl-debug.aar")
        file("$aarDir/intl.aar").renameTo("${rootProject.ext.outputDir}/hermes-intl-debug.aar")
      }
    }
    tasks.named("assembleRelease").configure {
      doLast {
        file("$aarDir/intl-release.aar").renameTo("${rootProject.ext.outputDir}/hermes-intl-release.aar")
        file("$aarDir/intl.aar").renameTo("${rootProject.ext.outputDir}/hermes-intl-release.aar")
      }
    }
  }
}
