/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

plugins {
  id("com.android.library")
}

def hermesWs = System.getenv("HERMES_WS_DIR")
def outputDir = "${hermesWs}/build_android/outputs"
def hermesC = "$hermesWs/build/ImportHermesc.cmake"
def abis = project.hasProperty("abis") 
  ? (project.getProperty("abis") instanceof String 
      ? project.getProperty("abis").split(",") 
      : project.getProperty("abis")) 
  : ["arm64-v8a", "armeabi-v7a", "x86_64", "x86"]

buildDir = "${hermesWs}/build_android/cppruntime"
buildDir.mkdirs()

android {
  namespace = "com.facebook.hermes.cppruntime"
  compileSdkVersion = 31

  defaultConfig {
    minSdkVersion = 21
    externalNativeBuild {
      cmake {
        arguments "-DANDROID_STL=c++_shared"
        arguments "-DANDROID_PIE=True"
      }
    }

    ndk {
      abiFilters (*abis)
    }
  }

  externalNativeBuild {
    cmake {
      version "3.22.1"
      path "src/main/cpp/CMakeLists.txt"
      buildStagingDirectory = "${hermesWs}/staging/cppruntime"
      buildStagingDirectory.mkdirs()
    }
  }

  packagingOptions {
    exclude "**/libstub.so"
  }

  afterEvaluate {
    // Gradle 4/5.0 outputs android-debug.aar and android-release.aar
    // Gradle 5.1 outputs android.aar for both
    // Unify the two by renaming the files afterwards.
    // Neither appear to care whether the original filename actually exists.
    def aarDir = "$buildDir/outputs/aar"
    tasks.named("assembleDebug").configure {
      doLast {
        file("$aarDir/cppruntime-debug.aar").renameTo("${outputDir}/hermes-cppruntime-debug.aar")
        file("$aarDir/cppruntime.aar").renameTo("${outputDir}/hermes-cppruntime-debug.aar")
      }
    }
    tasks.named("assembleRelease").configure {
      doLast {
        file("$aarDir/cppruntime-release.aar").renameTo("${outputDir}/hermes-cppruntime-release.aar")
        file("$aarDir/cppruntime.aar").renameTo("${outputDir}/hermes-cppruntime-release.aar")
      }
    }
  }
}
