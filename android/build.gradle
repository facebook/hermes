buildscript {
  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.3.0'
  }
}

apply plugin: 'com.android.library'

def facebookBuild = System.getenv("FACEBOOK") ?: "0"
def abis = ["arm64-v8a", "armeabi-v7a", "x86_64", "x86"]
def hermes_ws = System.getenv("HERMES_WS_DIR")

// For Facebook internal use:
def fbsource = System.getenv("FBSOURCE") ?:
    System.getenv("HOME") + "/fbsource"

if (hermes_ws == null || hermes_ws == "") {
  throw new InvalidUserDataException("HERMES_WS_DIR is not set")
}
buildDir = "${hermes_ws}/build_android"
buildDir.mkdirs()

android {
  compileSdkVersion = 28

  defaultConfig {
    externalNativeBuild {
      cmake {
        arguments "-DHERMES_IS_ANDROID=True"
        arguments "-DHERMES_FACEBOOK_BUILD=${facebookBuild}"
        arguments "-DANDROID_STL=c++_shared"
        arguments "-DANDROID_PIE=True"
        arguments "-DLLVM_BUILD_BASE=${hermes_ws}/llvm"
        arguments "-DLLVM_SRC_DIR=${hermes_ws}/llvm"
        arguments "-DFBSOURCE_DIR=${fbsource}"
        targets "libhermes"
      }
    }
    ndk {
      abiFilters (*abis)
    }
  }

  externalNativeBuild {
    cmake {
      version "3.6.0"
      path "../CMakeLists.txt"
      buildStagingDirectory = "${hermes_ws}/staging"
      buildStagingDirectory.mkdirs()
    }
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}
